import java_cup.runtime.*;
import java.util.*;

// ==========================
// CÓDIGO DEL PARSER
// ==========================

parser code {:
    //Crear Campeonato
    //Aca van variables globales (se pueden acceder desde cualquier parte del codigo)
    Campeonato campeonatoActual = null;
    Serie serieActual = null;

    Map<Integer, Partido> partidosPorNumero = new HashMap<>();

    List<Participante> participantes = new ArrayList<>();
:};


// ==========================
// TOKENS DEL LEXER
// ==========================

// Palabras clave
terminal CAMPEONATO, FIXTURE, SERIE, EQUIPOS;
terminal PARTIDO_NRO, PARTICIPANTE, PRONOSTICOS_PARTIDOS;

// Símbolos
terminal CORCHETE_ABRE, CORCHETE_CIERRE;
terminal COMA, DOSPUNTOS, GUION, SEPARADOR, DOBLE;

// Datos
terminal Integer NUMERO;
terminal String FECHA, EMAIL, ESTADIO, NOMBRE;

// ==========================
// NO TERMINALES
// ==========================

non terminal Object inicio;

non terminal Object campeonato;
non terminal Object fixture;

non terminal List<Serie> series;
non terminal Serie serie;

non terminal List<String> lista_equipos;
non terminal String equipo;

non terminal List<Partido> partidos;
non terminal Partido partido;

non terminal List<Participante> participantes;
non terminal Participante participante;

non terminal List<PronosticoPartido> pronosticos;
non terminal PronosticoPartido pronostico;

non terminal String nombre_compuesto;

// ==========================
// SÍMBOLO INICIAL
// ==========================

start with inicio;

// ==========================
// GRAMÁTICA
// ==========================

// --------------------------
// PROGRAMA COMPLETO
// --------------------------
inicio ::=
    campeonato
    fixture
    series
    participantes
    {:
        System.out.println(">>> Parseo COMPLETO: campeonato, fixture, series y participantes cargados correctamente.");
    :}
;

// --------------------------
// CAMPEONATO
// --------------------------
campeonato ::= CAMPEONATO nombre_compuesto:nombre NUMERO:anio
               {:
                    //Creamos el objeto de campeonato
                    String n = nombre;
                    int a = anio;

                    campeonatoActual = new Campeonato(n, a);

                    System.out.println(">>> Campeonato cargado: " + n + " (" + a + ")");
               :}
;

nombre_compuesto ::=
      NOMBRE:n
        {: RESULT = n; :}
    | nombre_compuesto:prev NOMBRE:n
        {: RESULT = prev + " " + n; :}
;


// --------------------------
// FIXTURE
// --------------------------
fixture ::=
    FIXTURE
    {:
        // (acciones fixture)
    :}
;

// --------------------------
// SERIES
// --------------------------
series ::=
      serie
    | series serie
;

// --------------------------
// SERIE
// --------------------------
serie ::= SERIE nombre_compuesto:nombre
          EQUIPOS CORCHETE_ABRE lista_equipos:equips CORCHETE_CIERRE
          partidos:partList
          {:
                //Creamos el objeto de seriie
                String nom = nombre;
                serieActual = new Serie(nom);

                // Agregamos los equipos cargados previamente
                List<String> equipos = equips;
                for (String e : equipos) serieActual.addEquipo(e);

                // Agregamos los partidos ya cargados
                List<Partido> partidos = partList;
                for (Partido p : partidos) serieActual.addPartido(p);

                campeonatoActual.agregarSerie(serieActual);
                System.out.println(">>> Serie cargada: " + nom + " con " + equipos.size() + " equipos y " + partidos.size() + " partidos.");
          :}
;


// --------------------------
// EQUIPOS
// --------------------------
equipo ::=
      NOMBRE:n
        {: RESULT = n; :}
    | equipo:prev NOMBRE:n
        {: RESULT = prev + " " + n; :}
;


lista_equipos ::= equipo:elem
                 {:
                     List<String> lista1 = new ArrayList<>();
                     lista1.add(elem);
                     RESULT = lista1;
                 :}
               | lista_equipos:lst COMA equipo:elem
                 {:
                     List<String> lista2 = lst;
                     lista2.add(elem);
                     RESULT = lista2;
                 :}
;


// --------------------------
// PARTIDOS
// --------------------------
partidos ::= partido:p
             {:
                List<Partido> lista1 = new ArrayList<>();
                lista1.add(p);
                RESULT = lista1;
             :}
           | partidos:lst partido:p
             {:
                List<Partido> lista2 = lst;
                lista2.add(p);
                RESULT = lista2;
             :}
;

partido ::= PARTIDO_NRO
            NUMERO:num
            FECHA:fecha GUION ESTADIO:est
            equipo:local NUMERO:gL GUION equipo:visitante NUMERO:gV
            {:
                int n = num;
                String f = fecha;
                String e = est;

                String equipoLocal = local;
                int golesEquipoLocal = gL;
                String equipoVisitante = visitante;
                int golesEquipoVisitante = gV;

                Partido p = new Partido(n, f, e, equipoLocal, golesEquipoLocal, equipoVisitante, golesEquipoVisitante);

                partidosPorNumero.put(n, p);

                RESULT = p;
            :}
;


// --------------------------
// PARTICIPANTES
// --------------------------
participantes ::= participante:p
                 {:
                    List<Participante> lista1 = new ArrayList<>();
                    lista1.add(p);
                    participantes = lista1;
                    RESULT = lista1;
                 :}
               | participantes:lst participante:p
                 {:
                    List<Participante> lista2 = lst;
                    lista2.add(p);
                    participantes = lista2;
                    RESULT = lista2;
                 :}
;


// --------------------------
// PARTICIPANTE
// --------------------------
participante ::= SEPARADOR
                  PARTICIPANTE
                  nombre_compuesto:nombre
                  GUION EMAIL:email
                  PRONOSTICOS_PARTIDOS
                  pronosticos:lista
                  {:
                    String nom = nombre;
                    String mail = email;

                    Participante participante = new Participante(nom, mail);

                    List<PronosticoPartido> l = lista;
                    for (PronosticoPartido pr : l)
                        participante.addPronostico(pr);

                    System.out.println(">>> Participante cargado: " + nom + " (" + mail + ") con " + l.size() + " pronósticos.");
                    RESULT = participante;
                  :}
;


// --------------------------
// PRONÓSTICOS
// --------------------------
pronosticos ::= pronostico:p
               {:
                    List<PronosticoPartido> lista1 = new ArrayList<>();
                    lista1.add(p);
                    RESULT = lista1;
               :}
             | pronosticos:lst pronostico:p
               {:
                    List<PronosticoPartido> lista2 = lst;
                    lista2.add(p);
                    RESULT = lista2;
               :}
;


// normal
pronostico ::= NUMERO:num DOSPUNTOS
                equipo:local NUMERO:gL GUION equipo:visitante NUMERO:gV
                {:
                    int n = num;

                    String equipoLocal = local;
                    int golesEquipoLocal = gL;
                    String equipoVisitante = visitante;
                    int golesEquipoVisitante = gV;

                    RESULT = new PronosticoPartido(n, equipoLocal, golesEquipoLocal, equipoVisitante, golesEquipoVisitante, false);
                :}
             // Para pronostico doble
             | NUMERO:num DOSPUNTOS DOBLE
                equipo:local NUMERO:gL GUION equipo:visitante NUMERO:gV
                {:
                    int n = num;

                    String equipoLocal = local;
                    int golesEquipoLocal = gL;
                    String equipoVisitante = visitante;
                    int golesEquipoVisitante = gV;

                    RESULT = new PronosticoPartido(n, equipoLocal, golesEquipoLocal, equipoVisitante, golesEquipoVisitante, true);
                :}
;
